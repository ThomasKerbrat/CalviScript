program              -> statementList EOI
statementList        -> (scopeBlock | statement)*
statementList_return -> (scopeBlock | return | statement)*
scopeBlock           -> '{' statementList '}'
statement            -> (variableDeclaration | assign | while | expression) ';'?
return               -> RETURN expression?

assign               -> iidentifier '=' expression
iidentifier          -> IDENTIFIER | variableDeclaration
variableDeclaration  -> 'var' IDENTIFIER

expression           -> functionDeclaration | (mathExpression ('?' expression ':' expression)?)
mathExpression       -> term (('+' | '-') term)*
term                 -> factor (('*' | '/' | '%') factor)*
factor               -> '-'? positiveFactor
positiveFactor       -> NUMBER | functionCall | IDENTIFIER | ('(' expression ')')

functionCall         -> IDENTIFIER '(' (expression (',' expression)*)? ')'
functionDeclaration  -> 'function' '(' (IDENTIFIER (',' IDENTIFIER)*)? ')' '{' statementList_return '}'

while                -> 'while' '(' expression ')' '{' statementList '}'
